model
{
    for (y in (A + a.min):(Y + A - 1)) {
        log.R[y] ~ dnorm(log.R.mean2[y], tau.R)
        R[y] <- exp(log.R[y])
        log.R.mean1[y] <- log(S[y - a.max]) + lnalpha - beta * 
            S[y - a.max] - gam * Fry[y - a.max]
        log.resid[y] <- log(R[y]) - log.R.mean1[y]
    }
    log.R.mean2[A + a.min] <- log.R.mean1[A + a.min] + phi * 
        log.resid.0
    for (y in (A + a.min + 1):(Y + A - 1)) {
        log.R.mean2[y] <- log.R.mean1[y] + phi * log.resid[y - 
            1]
    }
    lnalpha ~ dnorm(0.00000E+00, 1.00000E-06)  T(0.00000E+00, 3)
    beta ~ dnorm(0.00000E+00, 1.00000E-06)  T(0.00000E+00, )
    phi ~ dnorm(0.00000E+00, 1.00000E-06)  T(-0.98, 0.98)
    mean.log.RO ~ dnorm(0.00000E+00, 1.00000E-06)
    tau.RO ~ dgamma(0.001, 0.001)
    log.resid.0 ~ dnorm(0.00000E+00, tau.red)
    tau.R ~ dgamma(0.001, 0.001)
    gam ~ dnorm(0.00000E+00, 1.00000E-06)  T(-0.98, 0.98)
    sigma.R <- 1/sqrt(tau.R)
    alpha <- exp(lnalpha)
    sigma.RO <- 1/sqrt(tau.RO)
    tau.red <- tau.R * (1 - phi * phi)
    lnalpha.c <- lnalpha + (sigma.R * sigma.R/2/(1 - phi * phi))
    R.O <- exp(mean.log.RO)
    for (y in 1:a.max) {
        log.R[y] ~ dnorm(mean.log.RO, tau.RO)
        R[y] <- exp(log.R[y])
    }
    S.max <- 1/beta
    alpha.c <- min(exp(lnalpha.c), 10000)
    S.eq.c <- lnalpha.c * S.max
    U.msy.c <- lnalpha.c * (0.5 - 0.07 * lnalpha.c)
    S.msy.c <- S.eq.c * (0.5 - 0.07 * lnalpha.c)
    positive.lna.c <- step(lnalpha.c)
    lnalpha.c.nonneg <- lnalpha.c * positive.lna.c
    S.eq.c2 <- lnalpha.c.nonneg * S.max
    peterman.approx.c <- (0.5 - 0.65 * pow(lnalpha.c.nonneg, 
        1.27)/(8.7 + pow(lnalpha.c.nonneg, 1.27)))
    U.msy.c2 <- lnalpha.c.nonneg * peterman.approx.c
    S.msy.c2 <- U.msy.c2/beta
    U.max.c2 <- 1 - 1/exp(lnalpha.c.nonneg)
    D.scale ~ dunif(0.00000E+00, 1)
    D.sum <- 1/(D.scale * D.scale)
    pi.2p ~ dbeta(1, 1)
    pi.1 ~ dbeta(1, 1)
    pi[1] <- pi.1
    pi[2] <- pi.2p * (1 - pi[1])
    pi[3] <- 1 - pi[1] - pi[2]
    for (a in 1:A) {
        gamma[a] <- D.sum * pi[a]
        for (y in 1:(Y + A - 1)) {
            g[y, a] ~ dgamma(gamma[a], 0.001)
            p[y, a] <- g[y, a]/sum(g[y, ])
        }
    }
    for (a in 1:A) {
        for (y in a:(Y + (a - 1))) {
            N.ya[y - (a - 1), (A + 1 - a)] <- p[y, (A + 1 - a)] * 
                R[y]
        }
    }
    for (y in 1:Y) {
        N[y] <- sum(N.ya[y, 1:A])
        for (a in 1:A) {
            q[y, a] <- N.ya[y, a]/N[y]
        }
    }
    for (t in 1:Y) {
        x[t, ] ~ dmulti(q[t, ], n.a[t])
    }
    for (y in 1:Y) {
        mu.Hbelow[y] ~ dbeta(1, 1)
        H.below[y] <- mu.Hbelow[y] * N[y]
        log.Hb[y] <- log(H.below[y])
        tau.log.Hb[y] <- 1/log(Hbelow.cv[y] * Hbelow.cv[y] + 
            1)
        Hbelow[y] ~ dlnorm(log.Hb[y], tau.log.Hb[y])
        S[y] <- max(N[y] - H.below[y], 1)
        log.S[y] <- log(S[y])
    }
    log.q.weir ~ dnorm(0.00000E+00, 1.00000E-04)
    log.q.mr ~ dnorm(0.00000E+00, 1.00000E-04)
    for (y in 1:Y) {
        tau.log.weir[y] <- 1/log(weir.cv[y] * weir.cv[y] + 1)
        tau.log.mr[y] <- 1/log(mr.cv[y] * mr.cv[y] + 1)
        log.qS.weir[y] <- log.q.weir + log.S[y]
        log.qS.mr[y] <- log.q.mr + log.S[y]
        weir[y] ~ dlnorm(log.qS.weir[y], tau.log.weir[y])
        mr[y] ~ dlnorm(log.qS.mr[y], tau.log.mr[y])
        tau.log.ds[y] <- 1/log(DS.cv[y] * DS.cv[y] + 1)
        DS[y] ~ dlnorm(log.S[y], tau.log.ds[y])
        qS.mr[y] <- exp(log.qS.mr[y])
        qS.weir[y] <- exp(log.qS.weir[y])
    }
    q.mr <- exp(log.q.mr)
    q.weir <- exp(log.q.weir)
}
